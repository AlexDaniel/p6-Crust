#!/usr/bin/env perl6-m
use v6;

use HTTP::Server::Tiny;
use Crust;
use Getopt::Tiny;

my $opts = {
    host => '127.0.0.1',
    port => 5000,
    I => [],
};

get-options(
    $opts, <
        e=s
        I=s@
        p|port=i
        h|host=s
        accesslog=!
        v|version=!
    >
);

@*INC.prepend: @($opts<I>);

if $opts<version> {
    say "perl6 version {$*PERL.compiler.version} built on {$*VM.name} version {$*VM.version}";
    # TODO: show crust's version. but I don't know how to get it.
    exit 1;
}

my $httpd = HTTP::Server::Tiny.new($opts<host>, $opts<port>);

# eval
my $app = do {
    if $opts<e> {
        EVAL($opts<e>);
    } elsif @*ARGS.elems > 0 {
        EVALFILE(@*ARGS[0])
    } else {
        EVALFILE('app.psgi')
    }
};
if $opts<accesslog> {
    require Crust::Middleware::AccessLog;
    $app = ::('Crust::Middleware::AccessLog').new(app => $app);
}
$httpd.run($app);
