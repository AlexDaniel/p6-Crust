#!/usr/bin/env perl6-m
use v6;

use HTTP::Server::Tiny;
use Crust;
use Getopt::Tiny;

my $opts = {
    host => '127.0.0.1',
    port => 5000,
    I => [],
    M => [],
    s => 'HTTP::Server::Tiny',
};

get-options(
    $opts, <
        e=s
        I=s@
        M=s@
        p|port=i
        h|host=s
        accesslog=!
        v|version=!
        s=s
    >
);

@*INC.prepend: @($opts<I>);

if $opts<M> {
    for $opts<M> {
        # XXX I don't want to use string eval. please tell me the right way!
        EVAL "use $_";
    }
}

if $opts<version> {
    say "perl6 version {$*PERL.compiler.version} built on {$*VM.name} version {$*VM.version}";
    # TODO: show crust's version. but I don't know how to get it.
    exit 1;
}


# eval
my $app = do {
    if $opts<e> {
        EVAL($opts<e>);
    } elsif @*ARGS.elems > 0 {
        EVALFILE(@*ARGS[0])
    } else {
        EVALFILE('app.psgi')
    }
};
if $opts<accesslog> {
    require Crust::Middleware::AccessLog;
    $app = ::('Crust::Middleware::AccessLog').new(app => $app);
}

# XXX I don't want to use string eval. please tell me the right way!
EVAL "use Crust::Handler::{$opts<s>}";
my $httpd = ::("Crust::Handler::{$opts<s>}").new(|$opts);
$httpd.run($app);

