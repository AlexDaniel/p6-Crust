#!/usr/bin/env perl6-m
use v6;

use HTTP::Server::Tiny;
use Crust;
use Getopt::Tiny;

my $opts = {
    host => '127.0.0.1',
    port => 5000,
    I => [],
    M => [],
    s => 'HTTP::Server::Tiny',
};

get-options(
    $opts, <
        e=s
        I=s@
        M=s@
        p|port=i
        h|host=s
        accesslog=!
        lint=!
        v|version=!
        s=s
    >
);

@*INC.prepend: @($opts<I>);

if $opts<M> {
    for @($opts<M>) {
        require ::($_);
    }
}

if $opts<version> {
    say "perl6 version {$*PERL.compiler.version} built on {$*VM.name} version {$*VM.version}";
    # TODO: show crust's version. but I don't know how to get it.
    exit 1;
}


# eval
my $app = do {
    if $opts<e> {
        EVAL($opts<e>);
    } elsif @*ARGS.elems > 0 {
        EVALFILE(@*ARGS[0])
    } else {
        EVALFILE('app.psgi')
    }
};
if $opts<accesslog> {
    require Crust::Middleware::AccessLog;
    $app = ::('Crust::Middleware::AccessLog').new(app => $app);
}
if $opts<lint> {
    require Crust::Middleware::Lint;
    $app = ::('Crust::Middleware::Lint').new(app => $app);
}

my $handler = "Crust::Handler::{$opts<s>}";
require ::($handler);
my $httpd = ::($handler).new(|$opts);
$httpd.run($app);

=begin pod

=head1 NAME

crustup -  Run PSGI application with Crust handlers

=head1 SYNOPSIS

    # read your app from app.psgi file
    crustup

    # choose .psgi file from ARGV[0] (or with -a option)
    crustup hello.psgi

    # switch server implementation with --server (or -s)
    crustup --server HTTP::Server::Simple --port 9090 --host 127.0.0.1 test.psgi

=head1 DESCRIPTION

crustup is a command line utility to run PSGI applications from the
command line.

=end pod
